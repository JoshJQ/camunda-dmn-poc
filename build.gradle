plugins {
    id 'org.springframework.boot' version '2.3.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'nebula.release' version '15.3.1'
    id 'com.diffplug.spotless' version '5.10.2'
    id 'name.remal.sonarlint' version '1.2.2'
    id 'name.remal.test-source-sets' version '1.1.6'
}

group 'com.db'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

bootJar {
    manifest {
        attributes(
                'Implementation-Title': 'Camunda DMN POC',
                'Implementation-Version': archiveVersion
        )
    }
}

sonarlint {
    excludes {
        message 'java:S5960'
        message 'java:S1186'
    }
}

spotless {
    format 'misc', {
        target '*.gradle', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.8').aosp()
    }
}

ext {
    dmnXlsxConverterVersion = '0.2.0'
    lombokVersion = '1.18.12'
    jaxbApiVersion = '2.3.2'
    camundaVersion = '7.15.0-alpha2'
    h2Version = '1.4.200'
    cucumberVersion = '6.8.1'
    cucumberReportVersion = '4.0.96'
    restAssuredVersion = '4.2.0'
    jaxbOsgiVersion = '3.0.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation("org.camunda.bpm.extension.dmn:dmn-xlsx-converter:$dmnXlsxConverterVersion") {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    }
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:$jaxbApiVersion"
    implementation "org.glassfish.jaxb:jaxb-runtime:$jaxbApiVersion"

    implementation "org.camunda.bpm.dmn:camunda-engine-dmn:$camundaVersion"

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    implementation "com.h2database:h2:$h2Version"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation "io.cucumber:cucumber-java:$cucumberVersion"
    testImplementation "io.cucumber:cucumber-junit:$cucumberVersion"
    testImplementation("de.monochromata.cucumber:reporting-plugin:$cucumberReportVersion") {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    testImplementation "io.rest-assured:rest-assured:$restAssuredVersion"
    testImplementation "io.rest-assured:json-path:$restAssuredVersion"
    testImplementation "io.rest-assured:xml-path:$restAssuredVersion"
    testImplementation "com.sun.xml.bind:jaxb-osgi:$jaxbOsgiVersion"
}

test {
    useJUnitPlatform()
}
